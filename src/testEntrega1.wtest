import musicos.*
import canciones.*
import guitarras.*
import establecimientos.*
import presentaciones.*
import albumes.*
import builders.*
import exceptions.*

describe "Test entrega 1"{
	const joaquin
	const lucia
	const soledad
	const kike
	const fender
	const gibsonSana
	const gibsonNoSana
	const cancionCisne
	const cancionLaFamilia
	const cancionEres
	const cancionCorazonAmericano
	const cancionAlmaDeDiamante
	const cancionCrisantemo
	const albumEspecialLaFamilia
	const albumLaSole
	const albumParaLosArboles
	const albumJustCrisantemo
	const presentacionLunaPark
	const presentacionLaTrastienda
	const letraRemixLaFamilia
	const letraAlmaCrisantemo
	var recitalPdpalooza
	fixture {
		joaquin = new MusicoDeGrupoBuilder()
			.habilidadBase(20)
			.habilidadSiTocaEnGrupo(5)
			.tocaEnGrupo(true)
			.build()
		kike = new MusicoDeGrupoBuilder()
			.habilidadBase(60)
			.habilidadSiTocaEnGrupo(20)
			.tocaEnGrupo(true)
			.build()
		lucia = new VocalistaPopularBuilder()
			.habilidadBase(70)
			.habilidadSiTocaEnGrupo(-20)
			.tocaEnGrupo(true)
			.palabraMagica("familia")
			.build()
		soledad = new VocalistaPopularBuilder()
			.habilidadBase(55)
			.habilidadSiTocaEnGrupo(0)
			.tocaEnGrupo(false)
			.palabraMagica("amor")
			.build()
		fender = new Guitarra(10)
		gibsonSana = new Guitarra(15)
		gibsonNoSana = new Guitarra(5)
		cancionCisne = new Cancion("Cisne", 312, "Hoy el viento se abrió quedó vacío el aire una vez más y el manantial brotó y nadie está aquí y puedo ver que solo estallan las hojas al brillar")
		cancionLaFamilia = new Cancion("La familia", 264, "Quiero brindar por mi gente sencilla, por el amor brindo por la familia")
		cancionEres = new Cancion("Eres", 145, "Eres lo mejor que me pasó en la vida, no tengo duda, no habrá más nada después de ti. Eres lo que le dio brillo al día a día, y así será por siempre, no cambiará, hasta el final de mis días.")
		cancionCorazonAmericano = new Cancion("Corazón americano", 154, "Canta corazón, canta más alto, que tu pena al fin se va marchando, el nuevo milenio ha de encontrarnos, junto corazón, como soñamos.")
		cancionAlmaDeDiamante = new Cancion("Alma de diamante", 216, "Ven a mí con tu dulce luz alma de diamante. Y aunque el sol se nuble después sos alma de diamante. Cielo o piel silencio o verdad sos alma de diamante. Por eso ven así con la humanidad alma de diamante")
		cancionCrisantemo = new Cancion("Crisantemo", 175, "Tócame junto a esta pared, yo quede por aquí... cuando no hubo más luz... quiero mirar a través de mi piel... Crisantemo, que se abrió... encuentra el camino hacia el cielo")
		albumEspecialLaFamilia = new AlbumBuilder()
			.fecha(17, 6, 1992)
			.copiasVendidas(89000)
			.copiasEditadas(100000)
			.agregarCancion(cancionLaFamilia)
			.build()
		albumLaSole = new AlbumBuilder()
			.fecha(4, 2, 2005)
			.copiasVendidas(130000)
			.copiasEditadas(200000)
			.agregarCancion(cancionCorazonAmericano)
			.agregarCancion(cancionEres)
			.build()
		albumParaLosArboles = new AlbumBuilder()
			.fecha(31, 3, 2003)
			.copiasVendidas(49000)
			.copiasEditadas(50000)
			.agregarCancion(cancionCisne)
			.agregarCancion(cancionAlmaDeDiamante)
			.build()
		albumJustCrisantemo = new AlbumBuilder()
			.fecha(5, 12, 2007)
			.copiasVendidas(27500)
			.copiasEditadas(28000)
			.agregarCancion(cancionCrisantemo)
			.build()	
		presentacionLunaPark = new Presentacion(new Date(20,4,2017), lunaPark)
		presentacionLunaPark.cantantes().add(luisAlberto)
		presentacionLunaPark.cantantes().add(joaquin)
		presentacionLunaPark.cantantes().add(lucia)
		presentacionLaTrastienda = new Presentacion(new Date(15,11,2017), laTrastienda)
		presentacionLaTrastienda.cantantes().add(luisAlberto)
		presentacionLaTrastienda.cantantes().add(joaquin)
		presentacionLaTrastienda.cantantes().add(lucia)
		joaquin.albumes().add(albumEspecialLaFamilia)
		soledad.albumes().add(albumLaSole)
		luisAlberto.albumes().add(albumParaLosArboles)
		luisAlberto.albumes().add(albumJustCrisantemo)
		luisAlberto.guitarra(fender)
		/* FIXTURE TERCERA PARTE */
		letraRemixLaFamilia = "mueve tu cuelpo baby Quiero brindar por mi gente sencilla, por el amor brindo por la familia yeah oh yeah"

		letraAlmaCrisantemo = "Ven a mí con tu dulce luz alma de diamante. Y aunque el sol se nuble después sos alma de diamante. Cielo o piel silencio o verdad sos alma de diamante. Por eso ven así con la humanidad alma de diamante Tócame junto a esta pared, yo quede por aquí... cuando no hubo más luz... quiero mirar a través de mi piel... Crisantemo, que se abrió... encuentra el camino hacia el cielo"
		recitalPdpalooza = new Presentacion(new Date(15,12,2017), lunaPark)
	}
	
	/* A PARTIR DE AC� ES LA PRIMERA PARTE */
	test "Joaqu�n canta en el grupo Pimpinela. Queremos conocer su habilidad" {	
		assert.equals(25,joaquin.habilidad())
	}
	test "Joaqu�n canta en el grupo Pimpinela, y decide lanzar su carrera solista (deja el grupo). Queremos conocer
	su habilidad."{
		joaquin.tocaEnGrupo(false)
		assert.equals(20, joaquin.habilidad())
	}
	test "Luc�a canta en el grupo Pimpinela. Queremos saber
	su habilidad."{
		assert.equals(50,lucia.habilidad())
	}
	test "Luc�a canta en el grupo Pimpinela, y lanza su carrera solista (deja el grupo). Queremos conocer su
	habilidad."{
		lucia.tocaEnGrupo(false)
		assert.equals(70, lucia.habilidad())
	}
	test "Luis Alberto toca con la Fender. Queremos saber su
	habilidad."{
		luisAlberto.guitarra(fender)
		assert.equals(80, luisAlberto.habilidad())
	}
	test "Luis Alberto toca ahora con la Gibson sana.
	Queremos saber su habilidad."{
		luisAlberto.guitarra(gibsonSana)
		assert.equals(100, luisAlberto.habilidad())
	}
	test "Alguien trata mal a la Gibson y ahora no est� sana. Luis Alberto toca con la Gibson. Queremos saber su
	habilidad."{
		luisAlberto.guitarra(gibsonNoSana)
		assert.equals(40, luisAlberto.habilidad())
	}
	test "Queremos saber si Joaqu�n interpreta bien la canci�n La Familia"{
		assert.that(joaquin.interpretaBien(cancionLaFamilia))
	}
	test "Queremos saber si Joaqu�n interpreta bien la canci�n Cisne"{
		assert.that(joaquin.interpretaBien(cancionCisne))
	}
	test "Queremos saber si Luc�a interpreta bien la canci�n La Familia como solista"{
		lucia.tocaEnGrupo(false)
		assert.that(lucia.interpretaBien(cancionLaFamilia))
	}
	test "Queremos saber si Luc�a interpreta bien la canci�n Cisne como solista"{
		lucia.tocaEnGrupo(false)
		assert.that(lucia.interpretaBien(cancionCisne))
	}
	test "Queremos saber si Luc�a interpreta bien la canci�n La Familia"{
		assert.that(lucia.interpretaBien(cancionLaFamilia))
	}
	test "Queremos saber si Luc�a interpreta bien la canci�n Cisne"{
		assert.notThat(lucia.interpretaBien(cancionCisne))
	}
	test "Queremos saber si Luis Alberto interpreta bien la canci�n La Familia"{
		assert.that(luisAlberto.interpretaBien(cancionLaFamilia))
	}
	test "Queremos saber si Luis Alberto interpreta bien la canci�n Cisne"{
		assert.that(luisAlberto.interpretaBien(cancionCisne))
	}
	test "Queremos saber cu�nto cobra Joaqu�n por la presentaci�n del Luna Park"{
		assert.equals(50,joaquin.ganancia(presentacionLunaPark))
	}
	test "Modificar la presentaci�n de la Trastienda para que solo act�e Joaqu�n.
	Queremos saber cu�nto cobra Joaqu�n por la presentaci�n de la Trastienda del 15/11/2017"{
		presentacionLaTrastienda.eliminarCantante(lucia)
		presentacionLaTrastienda.eliminarCantante(luisAlberto)
		assert.equals(100,joaquin.ganancia(presentacionLaTrastienda))
	}
	test "Queremos saber la capacidad de La Trastienda para el 05 de agosto del 2017"{
		const dia = new Date(5,8,2017)
		assert.equals(700, laTrastienda.capacidadTotal(dia))
	}
	test "Saber la capacidad de La Trastienda para el 08 de agosto del 2017" {
		const dia = new Date(8,8,2017)
		assert.equals(400, laTrastienda.capacidadTotal(dia))
	}
	test "Queremos saber cu�nto cobra Luc�a por la presentaci�n del Luna Park"{
		assert.equals(500, lucia.ganancia(presentacionLunaPark))
	}
	test "Queremos saber cu�nto cobra Luc�a por la presentaci�n de La Trastienda"{
		assert.equals(400, lucia.ganancia(presentacionLaTrastienda))
	}
	test "Queremos saber cu�nto cobra Luis Alberto por la presentaci�n del Luna Park"{
		assert.equals(1000, luisAlberto.ganancia(presentacionLunaPark))
	}
	test "Queremos saber cu�nto cobra Luis Alberto por la presentaci�n de La Trastienda"{
		assert.equals(1200, luisAlberto.ganancia(presentacionLaTrastienda))
	}
	test "Queremos saber el costo de la presentaci�n del Luna Park"{
		assert.equals(1550, presentacionLunaPark.costoTotal())
	}
	test "Queremos saber el costo de la presentaci�n de la Trastienda el 15/11/2017"{
		assert.equals(1650, presentacionLaTrastienda.costoTotal())
	}
}
	